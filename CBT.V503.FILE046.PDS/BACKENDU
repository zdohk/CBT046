BACKEND  TITLE 'BACKEND OF PACKRAT PROGRAM WHICH DOES THE GUT WORK'
* ------------------------------------------------------------------- *
*
*       A FEW CHANGES BY CBT. ALL ARE MARKED BY         AXC-CBT
*        1. CHECK FOR THE FOLOIWNG 1ST LEVELS:          AXC-CBT
*           SYS1. SYS2. CBT. PROD.                      AXC-CBT
*           (NULLIFIED BY SBG. - 2012/11/06)
*
* ------------------------------------------------------------------- *
*
**      AND A FEW CHANGES TO USE THE UCB SCAN SERVICES          ABL-UCB
**      BY BRUCE LELAND AT HITACHI; MARKED AS FOLLOWS --->      ABL-UCB
*
* ------------------------------------------------------------------- *
*                                                               ZOS-U
**      AND A FEW CHANGES FOR Z/OS UCBSCAN, MARKED AS --->      ZOS-U
**      SHOW RETURN CODE AND STATUS IF SCRATCH FAILS.           ZOS-U
**      HANDLE OBTAIN RETURN CODES GREATER THAN 8.              ZOS-U
*
* ------------------------------------------------------------------- *
*
*       UCB SCAN DONE WITH ULUT - UPDATED FOR TYPE 3 - Z/OS 2.2.
*
*       Module is now AMODE 31 and RMODE 24.
*       64-bit access mode IS REQUIRED within ULUINIT and ULUSCAN
*       macros.  Otherwise, only AMODE 31.
*
*       For SAM64 access, the HI ORDER 31-BIT byte must be cleared.
*       THE LLGTR Rx,Rx instruction is used for this purpose.
*
*       There is a lot of very old code in here.  I did not attempt
*       to clean it all up.
*
*         (Sam Golob - 2015/02/01 - sbgolob@cbttape.org)
*
* ------------------------------------------------------------------- *
         EJECT
         CNOP  0,4
BACKEND  CSECT
BACKEND  AMODE 31                                                  ULUT
BACKEND  RMODE 24                                                  ULUT
         SAVE  (14,12)
         ST    R13,SAVE+4-BACKEND(R15)
         BAL   R15,CONT-BACKEND(R15)
SAVE     DC    18F'0'
CONT     ST    R15,8(,R13)
         LR    R13,R15
         USING SAVE,R13
         L     R2,0(,R1)               A(COMMAREA)
         USING COMMAREA,R2
         DSECT
COMMAREA DS    0CL69
DSN      DS    CL44
         DS    CL5
CRTDT    DS    CL5
         DS    CL6
TRKS     DS    CL5
         DS    CL4
         ORG   COMMAREA
KEYWORD  DS    CL4
KEYDATA  DS    CL65
         ORG   COMMAREA
RESPONSE DS    CL69
         ORG
BACKEND  CSECT
         CLC   KEYWORD,=C'VOL '
         BE    NEWVOL
         OC    DSN,DSN
         BZ    DSNREQST
         B     SCRREQST
RETURN   L     R13,SAVE+4
         RETURN (14,12),RC=(15)
         EJECT
NEWVOL   MVC   CAMVOL,KEYDATA
         XC    COMMAREA,COMMAREA
         OBTAIN FMT4DSCB
         C     R15,=A(8)                                        ZOS-U
         BH    OBTFAIL                                          ZOS-U
         B     *+4(R15)
         B     GOTVTOC
         B     NOTMTD
         B     VTOCERR
OBTFAIL  MVC   RESPONSE(L'ERRMSG4),ERRMSG4                      ZOS-U
         CVD   R15,DBL                                          ZOS-U
         OI    DBL+7,X'0F'                                      ZOS-U
         UNPK  RESPONSE+15(3),DBL+6(2)                          ZOS-U
         MVI   RESPONSE+15,C' '                                 ZOS-U
         B     RETURN                                           ZOS-U
         DC    8H'0'
         SPACE 3
GOTVTOC  MVC   LASTFMT1,WORKA+1
         MVC   DSCBTRK+1(1),WORKA+30
         MVC   TRKSCYL,WORKA+20
         MVC   CCHHR(4),WORKA+63
         MVI   CCHHR+4,1
         SPACE 3
         L     4,16                  -> CVT
         TM    CVTDCB-CVT(R4),CVTMVSE  REALLY XA?               ABL-UCB
         BO    TRYXA                   YES, BRANCH              ABL-UCB
         L     4,40(4)               -> UCB LOOKUP TABLE
UCBLOOP  LH    3,0(4)                16 BIT (MVS STYLE) UCB ADDRESS
         N     3,=X'0000FFFF'        CLEAR IN CASE SIGN BIT PROPAGATED
         BZ    BUMPUCB               GET NEXT
         CH    3,=H'-1'              END OF TABLE
         BE    NOTMTD
         CLC   CAMVOL,28(3)          VOLSER
         BE    GOTUCB
BUMPUCB  LA    4,2(4)
         B     UCBLOOP
***                                                             ABL-UCB
         DS    0D                                               ABL-UCB
UCBPARMS DS    3A                 PARMS FOR UCB SCAN SERVICES   ABL-UCB
UCBANSR  DS    A                  RESULT UCB ADRESS             ABL-UCB
UCBAREA  DS    12F                RESULT COPY OF UCB            ZOS-U
         DS    0D                 ALIGN WORK AREA               ZOS-U
UCBWORK  DS    CL100              UCB SCAN WORK AREA            ABL-UCB
TRYXA    LA    R5,UCBWORK         WORK AREA ADDRESS             ABL-UCB
         XC    UCBWORK(100),UCBWORK  CLEAR THE WORK AREA-RESCAN ABL-UC2
         TM    CVTDCB-CVT(R4),CVTOSEXT    OSLEVEL EXTENSION?    ZOS-U
         BNO   USEXA              NO                            ZOS-U
         TM    CVTOSLV1-CVT(R4),CVTJ5522  AT LEAST ESA 5.2.2?      ULUT
         BO    UCBULUT            Use ULUT UCB lookup method.      ULUT
         TM    CVTOSLV0-CVT(R4),CVTUCBSV  GENERAL METHODS?      ZOS-U
         BNO   USEXA              NO                            ZOS-U
         B     UCBNEXT
* ------------------------------------------------------------------- *
* -- Use ULUT UCB scanning method with ULUINIT and ULUSCAN macros. -- *
* ------------------------------------------------------------------- *
UCBULUT  DS    0H                 USE MACROS TO SCAN UCB LOOKUP TABLE
         LLGTR R10,R10            CLEAR HI ORDER
         LLGTR R13,R13               33 BITS OF 64-BIT
         LLGTR R14,R14                  REGISTERS FOR SAM64
* ------------------------------------------------------------------- *
     ULUINIT WRKAREA=WORKU,UREG=R9,EXIT=NOTMTD,WR1=R3,WR2=R12,BREG=R10
* ------------------------------------------------------------------- *
* --  SCAN EACH UCB, REPRESENTED BY A ULUDSECT CONTROL BLOCK       -- *
* ------------------------------------------------------------------- *
LOOPUCB  DS    0H
         ULUSCAN WR1=R3,UCBR=R5,BREG=R10
* ------------------------------------------------------------------ *
         L     R5,ULUDUCBA        Get real UCB address for unit.   ULUT
         CLC   CAMVOL,28(R5)      Is this the volser we want?      ULUT
         BNE   LOOPJUMP           No. Look further.                ULUT
         LR    R3,R5              Load R3 with UCB addr for GOTUCB ULUT
         B     GOTUCB             Yes. Got the UCB for this disk.  ULUT
LOOPJUMP DS    0H                                                  ULUT
         MVC   ULUDCURR,ULUDNEXT  Get next UCB from ULUDSECT.
         BCT   R6,LOOPUCB         And loop until we've scanned all
         B     NOTMTD             Finished ULUT table w/o finding. ULUT
* ------------------------------------------------------------------ *
UCBNEXT  DS    0H                                                  ULUT
         LA    R3,UCBAREA                                       ZOS-U
         UCBSCAN COPY,WORKAREA=UCBWORK,DEVCLASS=DASD,RANGE=ALL,        X
               UCBAREA=UCBAREA,DYNAMIC=YES,VOLSER=CAMVOL        ZOS-U
         LTR   R15,R15            DID WE GET THE VOLUME?        ZOS-U
         BNZ   NOTMTD             NO.                           ZOS-U
         B     GOTUCB             YES.                          ZOS-U
USEXA    DC    0H'0'                                            ZOS-U
         LA    R6,=AL1(UCB3DACC)  DASD UCB'S DESIRED            ABL-UCB
         LA    R7,UCBANSR         RESULTANT UCB ADDRESS         ABL-UCB
         STM   R5,R7,UCBPARMS     SAVE PARAMETER LIST           ABL-UCB
         OI    UCBPARMS+8,X'80'   MARK END OF LIST              ABL-UCB
         SPACE 1                                                ABL-UCB
INCR1    LA    R1,UCBPARMS           START OF PARAMETER LIST    ABL-UCB
         L     R5,16                 A(CVT)                     ABL-UCB
         L     R15,CVTUCBSC-CVT(R5)  START OF UCB SCAN SERVICE  ABL-UCB
         BALR  R14,R15               GO SCAN UCB LIST           ABL-UCB
         SPACE 1                                                ABL-UCB
         LTR   R15,R15               END OF LIST?               ABL-UCB
         BNZ   NOTMTD                YES, BRANCH                ABL-UCB
         L     R3,UCBANSR            START OF THIS UCB          ABL-UCB
         CLC   CAMVOL,28(R3)         THIS VOLUME?               ABL-UCB
         BNE   INCR1                 NO, LOOP                   ABL-UCB
         PRINT NOGEN                                            ABL-UCB
         CVT   DSECT=YES                                        ABL-UCB
         IEFUCBOB ,                                             ABL-UCB
         PRINT GEN                                              ABL-UCB
BACKEND  CSECT ,                                                ABL-UCB
***                                                             ABL-UCB
GOTUCB   MVC   DEVTYPE,16(R3)
         B     DSNREQST
         SPACE 3
VTOCERR  XC    CAMVOL,CAMVOL
         MVC   RESPONSE(L'ERRMSG1),ERRMSG1
         B     RETURN
NOTMTD   XC    CAMVOL,CAMVOL
         MVC   RESPONSE(L'ERRMSG2),ERRMSG2
         B     RETURN
         EJECT
DSNREQST OC    CAMVOL,CAMVOL
         BNZ   VOLOK
         DC    H'0'
VOLOK    XC    COMMAREA,COMMAREA
NEXTDSCB BAL   8,CCHHRINC
         CLC   LASTFMT1,CCHHR
         BL    ENDOFVOL
         OBTAIN FMT1DSCB
         LTR   15,15
         BNZ   VTOCERR
         CLI   WORKA+44,C'1'
         BNE   NEXTDSCB
GOTF1    MVC   DSN,WORKA
         XR    3,3
         IC    3,WORKA+53         CRTDT(YR)
         CVD   3,DBL
         UNPK  CRTDT(2),DBL+6(2)
         OI    CRTDT+1,240
         LH    3,WORKA+54         CRTDT(DAY)
         CVD   3,DBL
         UNPK  CRTDT+2(3),DBL+6(2)
         OI    CRTDT+4,240
*
*
*
*       AREA FOR CALCULATION OF DS SIZE
*
*
         SR    R7,R7             CYLINDER COUNTER
         LR    R3,R7             TRACK COUNTER
         LR    R4,R7             EXTENT COUNTER
         MVC   CCSAVE,CCHHR      SAVE CCHHR
         LA    R5,WORKA+105      ---> TO 1ST EXTENT
         LA    R6,3              MAX OF 3 EXTENT IN FMT1 DSCB
EX1      BAL   R8,EXTENT         GO ACCUMULATE.
         B     DOEXT             NO MOVE
         BCT   R6,EX1            LOOP
EX2      OC    WORKA+135(5),WORKA+135 ANY MORE EXTENTS
         BZ    DOEXT             NO
         MVC   CCHHR,WORKA+135
         OBTAIN FMT1DSCB         GET FORMAT 2 OR 3 DSCB
         CH    R15,=H'4'         OK?
         BNL   DOEXT             NO IGNORE IT
         CLI   WORKA+44,C'3'     TYPE 3 ?
         BNE   EX2               NO TRY AGAIN.
         MVC   WORKA+44(90),WORKA+45 CONDENSE DSCB TO MAKE CONTIGUOUS
         LA    R5,WORKA+4        FIST SET
         LA    R6,13             MAX OF 13 EXTENTS LEFT
EX3      BAL   R8,EXTENT         GO ACCUMULATE
         B     DOEXT             NO MORE
         BCT   R6,EX3            LOOP
DOEXT    MH    R7,TRKSCYL        CONVERT CYLS TO TRACKS
         AR    R7,R3             +TRACKS
         CVD   R7,DBL             CONVERT
         UNPK  TRKS(5),DBL+5(3)   MOVE TO OUTPUT
         OI    TRKS+4,X'F0'       MAKE PRINTABLE
         MVC   CCHHR,CCSAVE       RESTORE CCHHR
         B     ENDOFVOL
EXTENT   DS    0H
*        R5 ---> CURRENT EXTENT POINTER
*        R7      CUMULATIVE CYLINDERS
*        R3      CUMULATIVE TRACKS
*        R4      NO OF EXTENTS
         CLI     0(R5),0         ANY MORE EXTENTS?
         BCR     8,R8             NO EXIT AND INDICATE
         CLI     0(R5),X'80'     SPLIT CYLINDERS?
         BNE     EXT1            NO
         MVI     TRKS+6,C'?'   FLAG SPACE AS INCORRECT
EXT1     MVC     DBL,2(R5)       ALIGN
         LA      R4,1(R4)        +1   EXTENT
         AH      R7,DBL+4        +CYL END
         AH      R3,DBL+6        +TRK END
         SH      R7,DBL          -CYL START
         SH      R3,DBL+2        -TRK START
         AH      R3,=H'1'        CORRECT NUMBER OF TRACKS
         LA      R5,10(R5)       NEXT FIELD
         B       4(R8)
         SPACE 3
ENDOFVOL XR    15,15
         B     RETURN
         EJECT
SCRREQST OC    CAMVOL,CAMVOL
         BNZ   SCROK
         DC    H'0'
SCROK    MVC   CAMDSN,DSN
         XC    COMMAREA,COMMAREA
*        CLC   CAMDSN(4),=C'SYS1'                          AXC-CBT
*        CLC   CAMDSN(5),=C'SYS1.'                         AXC-CBT
*        BE    NOSCR                                       AXC-CBT
*        CLC   CAMDSN(5),=C'SYS2.'                         AXC-CBT
*        BE    NOSCR                                       AXC-CBT
*        CLC   CAMDSN(5),=C'PROD.'                         AXC-CBT
*        BE    NOSCR                                       AXC-CBT
*        CLC   CAMDSN(4),=C'CBT.'                          AXC-CBT
*        BE    NOSCR                                       AXC-CBT
         LOCATE LOCATE
         LTR   15,15
         BNZ   NOUNCAT
         CLI   WORKA+1,1        MORE THAN 1 VOL?
         BNE   NOSCR            YES
         CLC   CAMVOL,WORKA+6   CATLGD ON DIFFERENT VOLUME?
         BNE   NOUNCAT          YES ... DON'T UNCAT
         CATALOG UNCATLG
         LTR   15,15
         BNZ   NOSCR            COULDN'T UNCATLG ... DON'T SCRATCH
NOUNCAT  XC    VOLSEQ,VOLSEQ    CLEAR JUST IN CASE USED
         LLGTR R0,R0
         SR    R0,R0            INDICATE NO UCB                 ZOS-U
*        L     R0,ULUDUCBA
         SCRATCH SCRATCH
         LTR   15,15
         BZ    GOODSCR
NOSCR    DS    0H
*        DC    H'0'
         MVC   DSN,CAMDSN
         MVC   RESPONSE+49(L'ERRMSG3),ERRMSG3
         CVD   R15,DBL             RETURN CODE FROM SCRATCH     ZOS-U
         OI    DBL+7,X'0F'                                      ZOS-U
         UNPK  RESPONSE+49+11(3),DBL+6(2)                       ZOS-U
         MVI   RESPONSE+49+11,C' '                              ZOS-U
         LH    R14,VOLLIST+12       GET STATUS CODE             ZOS-U
         N     R14,=A(255)                                      ZOS-U
         CVD   R14,DBL                                          ZOS-U
         OI    DBL+7,X'0F'                                      ZOS-U
         UNPK  RESPONSE+49+14(3),DBL+6(2)                       ZOS-U
         MVI   RESPONSE+49+14,C' '                              ZOS-U
         B     RETURN
GOODSCR  MVC   DSN,CAMDSN
         MVC   RESPONSE+49(L'SCRMSG1),SCRMSG1
         B    RETURN
         EJECT
CCHHRINC XR    1,1
         IC    1,CCHHR+4
         LA    1,1(1)
         STC   1,CCHHR+4
         CH    1,DSCBTRK
         BCR   13,8
         MVI   CCHHR+4,1
         LH    1,CCHHR+2
         LA    1,1(1)
         STH   1,CCHHR+2
         CH    1,TRKSCYL
         BCR   4,8
         XC    CCHHR+2(2),CCHHR+2
         LH    1,CCHHR
         LA    1,1(1)
         STH   1,CCHHR
         BR    8
         EJECT
FMT1DSCB CAMLST SEEK,CCHHR,CAMVOL,WORKA
FMT4DSCB CAMLST SEARCH,FMT4DSN,CAMVOL,WORKA
SCRATCH  CAMLST SCRATCH,CAMDSN,,VOLLIST,,OVRD
LOCATE   CAMLST NAME,CAMDSN,,WORKA
UNCATLG  CAMLST UCATDX,CAMDSN
         EJECT
CAMDSN   DC    CL44' '
VOLLIST  DC    H'1'
DEVTYPE  DS    XL4
CAMVOL   DC    CL6' '
VOLSEQ   DC    H'0'
CCHHR    DC    XL5'0'
CCSAVE   DC    XL5'0'
         CNOP  0,4
WORKA    DS    265C
         DS    0D
WORKU    DS    88C
FMT4DSN  DC    44X'04'
LASTFMT1 DC    XL5'0'
DSCBTRK  DC    H'0'
TRKSCYL  DC    H'0'
ERRMSG1  DC    C'ERROR ACCESSING VTOC'
ERRMSG2  DC    C'ERROR ACCESSING VOLUME'
ERRMSG3  DC    CL20'NOT DELETED'
ERRMSG4  DC    CL20'OBTAIN ERROR RC XX'                         ZOS-U
SCRMSG1  DC    CL20'GONE'
DBL      DC    D'0'
         LTORG
**                           EQUATES FOR SYMBOLIC REG USAGE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         END
