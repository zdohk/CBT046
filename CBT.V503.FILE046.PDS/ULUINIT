         MACRO
         ULUINIT &WRKAREA=,&UREG=,&EXIT=,&BREG=,&WR1=,&WR2=
* ------------------------------------------------------------------ *
*   ULUINIT - Access the ULUT (UCB Lookup Table) and initialize      *
*             the ULUDSECT control block with some of the ULUT       *
*             information.                                           *
* ------------------------------------------------------------------ *
*                                                                    *
*   Important:  If you are running with ULUT Type=3 or higher,       *
*   ---------   this macro invokes the SAM64 instruction.            *
*                                                                    *
*       Therefore, YOU MUST CLEAR bit 33 X'00000000_80000000'        *
*       from the BASE REGISTERS before invoking this macro.          *
*                                                                    *
*       Example:  If the base register is R10, then code:            *
*                                                                    *
*          LLGTR    R10,R10                                          *
*          ULUINIT  etc, etc.                                        *
*                                                                    *
*       Otherwise you may get a S0C4, reason code 3B.                *
*                                                                    *
* ------------------------------------------------------------------ *
*    The ULUINIT macro must be invoked in AMODE 31.                  *
*    The ULUINIT macro initializes the ULUDSECT control block.       *
*    The user must supply a 88 bit workarea for ULUINIT to use.      *
*    The ULUDSECT work area may reside in 24 bit storage.            *
* ------------------------------------------------------------------ *
*        Registers indicated by WRn will be saved and restored.      *
* ------------------------------------------------------------------ *
*                                                                    *
*    Parameter descriptions:                                         *
*                                                                    *
*        WRKAREA - a 88 byte work area to contain the data           *
*                  described by the ULUDSECT control block           *
*        UREG    - the USING register for accessiblity to the        *
*                  ULUDSECT control block in the work area           *
*        EXIT    - a location to escape the ULUT access if           *
*                  there is an error                                 *
*        BREG    - BCT register initially containing ULUITOTC        *
*                  to count table entries as you pass through        *
*                  them.  May not have to be decremented by BCT.     *
*                  But it should be decremented for each ULUT        *
*                  entry that was passed through.                    *
*        WR1     - work register - saved and restored  31- or 64-bit *
*        WR2     - work register - saved and restored  31- or 64-bit *
*                                                                    *
* ------------------------------------------------------------------ *
*                                                                    *
*        Enter and leave this macro in 31-bit mode, always.          *
*        -----     -----               -----------                   *
* ------------------------------------------------------------------ *
         LA    &UREG,&WRKAREA         Point to work area
         USING ULUDSECT,&UREG
*                                     Get ULUT and initialize.
         ST    &UREG,ULUDSVUR         Save USING register in cntl blk.
         STG   &WR1,ULUDSVW1          Save off entire two bytes.
         STG   &WR2,ULUDSVW2          Save off entire two bytes.
         ST    &BREG,ULUDSVBR         Save count register in cntl blk.
         LLGTR &WR1,&WR1              Make sure hi order not corrupted
         LLGTR &WR2,&WR2
         L     &WR1,16                CVT
         MVC   ULUDSLV6(1),X'4F6'(&WR1)  Move entire CVTOSLV6 to DSECT
*                                        Will use as a switch, later.
         LLGTR &WR1,&WR1              Make sure hi order not corrupted
         LLGTR &WR2,&WR2
*                                     ****  GET THE ULUT NOW  ****
         L     &WR1,X'7C'(,&WR1)      CVTIXAVL
         L     &WR1,X'D0'(,&WR1)      IOVT
*                                     Check if IOVTUL31 is zero or not.
*                                     If zero, then IOVTULUT is nonzero
         L     &WR2,X'8'(,&WR1)       Load IOVTUL31 fullword.
         LTR   &WR2,&WR2              Is it zero?
         BNZ   ULUNTYP3               No. We have Type 1 or 2.
*                                     Yes. Handle 64-bit ULUT
         OI    ULUDSLV6,X'40'         Indicate 64-bit ULUT table.
         LG    &WR2,X'1B8'(,&WR1)     Load dwd ULUT address in 2nd reg
         LGR   &WR1,&WR2              Copy dwd address to first reg
* ------------------------------------------------------------------ *
         SAM64                        Deal with ULUT in 64-bit mode
         CLC   =C'ULUT',0(&WR1)       Is ULUT eyecatcher there?
         BNE   &EXIT                  No eyecatcher, not ULUT, exit.
         STG   &WR1,ULUDULUT          Save ULUT address in DSECT
         CLI   4(&WR1),X'03'          Is the Type = X'03' ?
         BE    ULUITYP3               Yes. Deal with Type 3.
         SAM31                        31-bit in anticipation of exit
         B     &EXIT                  No. exit.
ULUITYP3 DS    0H                     Handle Type 3
         MVI   ULUDTYPE,X'03'         Indicate ULUT Type 3.
         SAM64                        Has to be 64-bit mode
         LG    &WR1,X'10'(,&WR1)      Type 3, addr of first table entry
         STG   &WR1,ULUDCURR          Store 64-bit entry in cntl block
         L     &WR2,X'1C'(,&WR2)      Count of all entries - fullword
         LLGTR &WR2,&WR2              Clear hi order of WR2.
         SAM31                        Nothing later needs SAM64
         ST    &WR2,ULUDTOTC          Store in control block
         ST    &WR2,ULUDRUNC          Initialize running descent count
         B     ULUILSET               Do search processing.
ULUNTYP3 DS    0H
         L     &WR2,8(,&WR1)          UCB LOOKUP TABLE (ULUT)
         LR    &WR1,&WR2              Point both registers to it.
         CLC   =C'ULUT',0(&WR1)       Eyecatcher there?
         BNE   &EXIT                  Nope. Not pointing to table.
         ST    &WR1,ULUDULUT          Start filling control block.
         CLI   4(&WR1),X'01'          Type 1?
         BE    ULUITYP1               Yes. Handle as type 1.
         CLI   4(&WR1),X'02'          Type 2?
         BE    ULUITYP2               Yes. Handle as type 2.
         B     &EXIT                  No. If new type, test for it here
ULUITYP1 DS    0H                     Handle Type 1
         MVI   ULUDTYPE,X'01'         Move type to control block.
         L     &WR1,X'0C'(,&WR1)      TYPE 1, addr of first entry
         ST    &WR1,ULUDCURR          Store entry addr in control block
         L     &WR2,X'10'(,&WR2)      Count of all entries.
         ST    &WR2,ULUDTOTC          Put number into control block.
         ST    &WR2,ULUDRUNC          Initialize running descend count
         B     ULUILSET               Do search processing.
*
ULUITYP2 DS    0H                     Handle Type 2
         MVI   ULUDTYPE,X'02'         Move type to control block.
         L     &WR1,X'14'(,&WR1)      TYPE 2, addr of first entry
         ST    &WR1,ULUDCURR          Store entry addr in control block
         L     &WR2,X'18'(,&WR2)      Count of all entries.
         ST    &WR2,ULUDTOTC          Put number into control block.
         ST    &WR2,ULUDRUNC          Initialize running descend count
         B     ULUILSET               Do search processing.
*
ULUILSET DS    0H                     Must exit in 31-bit mode.
         L     &BREG,ULUDTOTC         Load number of loops in register
         LG    &WR1,ULUDSVW1          Restore work register contents
         LG    &WR2,ULUDSVW2
*
         ULUDSECT
&SYSECT  CSECT ,                      Resume user's CSECT
         MEND
