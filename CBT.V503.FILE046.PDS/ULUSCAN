         MACRO
         ULUSCAN &WR1=,&UCBR=,&BREG=
* ------------------------------------------------------------------ *
*   ULUSCAN - Access one ULUT (UCB Lookup Table) entry and fill      *
*             in all the rest of the fields of the ULUDSECT control  *
*             block, which depend on the current particular ULUT     *
*             table entry.                                           *
* ------------------------------------------------------------------ *
*    The ULUSCAN macro must be invoked in AMODE 31.                  *
*    The user must supply a 88 bit workarea which contains the       *
*      data described by the ULUDSECT macro.                         *
*    The ULUDSECT work area may reside in 24 bit storage.            *
* ------------------------------------------------------------------ *
*        Registers indicated by WRn will be saved and restored.      *
* ------------------------------------------------------------------ *
*                                                                    *
*    Parameter descriptions:                                         *
*                                                                    *
*        UCBR    - the register pointing to the current UCB          *
*                  pointed to by this ULUT entry                     *
*        BREG    - BCT register initially containing ULUDTOTC        *
*                  to count table entries as you pass through        *
*                  them.  Later contains descending count,           *
*                  ULUDRUNC.                                         *
*        WR1     - work register - saved and restored                *
*                                                                    *
* ------------------------------------------------------------------ *
         TM    ULUDSLV6,X'40'         Is this a 64-bit ULUT?
         BZ    SKIPTY3                No. Deal as 31-bit.
         STG   &WR1,ULUDSVW1          Save work reg previous contents
*                                     64-bit housekeeping
         LLGTR &WR1,&WR1              Clear 64-bit hi order for safety
         LLGTR &UCBR,&UCBR            Clear 64-bit hi order for safety
         LLGTR &BREG,&BREG            Clear 64-bit hi order for safety
         LG    &WR1,ULUDCURR          Look at current ULUT entry
         B     SKIPTY2
*                                     31-bit processing
SKIPTY3  DS    0H
         ST    &WR1,ULUDSVW1          Save work reg previous contents
         L     &WR1,ULUDCURR          Look at current ULUT entry
*
SKIPTY2  DS    0H                     Both modes come here.
         ST    &BREG,ULUDRUNC         Save descending count.
         TM    ULUDSLV6,X'40'         64-bit ULUT?
         BZ    SKIPTY3A               No. Stay in 31-bit mode.
         SAM64
SKIPTY3A DS    0H                     Both modes come here.
         MVC   ULUDDEVA(2),0(&WR1)    Get hex device address in entry
         MVC   ULUDCNTR(2),4(&WR1)    Get counter of this ULUT entry
         L     &UCBR,8(,&WR1)         Get address of current UCB
         ST    &UCBR,ULUDUCBA         Save address in control block
         MVC   ULUDONLF(1),3(&UCBR)   Save byte to show if online
         MVC   ULUDDEVT(4),X'10'(&UCBR) Copy device type to ctrl block
*                                   Get address of UCB common extension
         TM    ULUDSLV6,X'40'         64-bit mode?
         BZ    SKIPTY3B               No. No need to SAM31.
         SAM31
SKIPTY3B DS    0H
         ST    R1,ULUDSVR1            Save R1 in control block
         L     R1,ULUDUCBA            Load real UCB address
         ST    R0,ULUDSVR0            Save register 0
         IOSCMXR
         ST    R1,ULUDCEXT            Result of macro, UCB common ext.
         L     R0,ULUDSVR0            Restore register 0
         L     R1,ULUDSVR1            Restore register 1
         TM    ULUDSLV6,X'40'         Running z/OS 2.2 or higher?
         BZ    SKIPTY3C               No. Don't switch modes.
         SAM64                        ULUT address is 64-bit
SKIPTY3C DS    0H
         LA    &WR1,X'C'(,&WR1)       Go to next ULUT entry
         TM    ULUDSLV6,X'40'         Are we running a 64-bit ULUT?
         BZ    SKIPTY3D               No. Handle fullword processing.
         STG   &WR1,ULUDNEXT          Doubleword processing.
         LG    &WR1,ULUDSVW1
         SAM31                        Switch to 31-bit before exit.
         B     SKIPEXIT
SKIPTY3D DS    0H                     Fullword processing 31-bit ULUT.
         ST    &WR1,ULUDNEXT          And save it in control block
         L     &WR1,ULUDSVW1          Restore prev work reg contents
SKIPEXIT DS    0H
         MEND
